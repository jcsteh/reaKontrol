# ReaKontrol
# SConscript for building of the source
# Author: James Teh <jamie@jantrid.net>
# Copyright 2018 James Teh
# License: GNU General Public License version 2.0

Import("env")
env.Append(CPPPATH=("#include", "#include/WDL"))

sources = [
	"fxMap.cpp",
	"main.cpp",
	"niMidi.cpp",
	"mcu.cpp",
]

if env["PLATFORM"] == "win32":
	# On Windows, OSARA is build with LLVM to have a toolchain that's closer to what's used on Mac
	env["CC"] = "clang-cl"
	env["LINK"] = "lld-link"
	env.Append(CXXFLAGS=[
		"/clang:-std=c++20",
		"/EHsc",
	])
	libs = ["winmm", "SetupAPI"]

	# We always want debug symbols.
	env.Append(PDB="${TARGET}.pdb")
	# having symbols usually turns this off, but we have no need for unused symbols.
	env.Append(LINKFLAGS='/OPT:REF')

else: # Mac
	swellDir = env.Dir("#include/WDL/WDL/swell")
	env.Append(CPPPATH=[swellDir])
	sources.append(swellDir.File("swell-modstub.mm"))
	libs = []
	env["CXX"] = "clang++"
	coreFlags = ("-mmacosx-version-min=10.7 -stdlib=libc++ "
		"-arch x86_64 -arch arm64")
	cxxFlags = coreFlags + " -std=c++20"
	env.Append(CXXFLAGS=cxxFlags)
	env.Append(LINKFLAGS=coreFlags)
	env.Append(CPPDEFINES="SWELL_PROVIDED_BY_APP")
	env.Append(LINKFLAGS="-framework Appkit")
	# We don't want the "lib" prefix for our dylib.
	env["SHLIBPREFIX"] = ""

env.SharedLibrary(
	target="reaper_kontrol",
	source=sources, LIBS=libs,
)
